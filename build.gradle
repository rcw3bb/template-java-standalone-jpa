plugins {
    id 'application'
    id "org.javamodularity.moduleplugin" version "1.8.12"
    id 'org.beryx.jlink' version '2.25.0'
}

group = 'xyz.ronella.template'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

application {
    mainModule = moduleName
    mainClass = 'xyz.ronella.template.jpa.Main'
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'jakarta.persistence:jakarta.persistence-api:3.0.0'
    implementation('jakarta.xml.bind:jakarta.xml.bind-api:3.0.1') {
        exclude group: 'com.sun.activation', module: 'jakarta.activation'
    }
    implementation('org.hibernate:hibernate-core-jakarta:5.6.15.Final') {
        exclude group: 'com.sun.activation', module: 'jakarta.activation'
    }

    implementation 'com.h2database:h2:2.1.214'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

ext {
    packageName = "${project.name}-${version}"
}

test {
    useJUnitPlatform()
}

jlink {
    options.addAll(['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'])

    launcher{
        moduleName = application.mainModule
        mainClassName = application.mainClass
    }

    mergedModule {
        additive = true
        excludeRequires 'jakarta.activation'
        uses 'org.hibernate.integrator.spi.Integrator'
        uses 'org.hibernate.boot.registry.selector.StrategyRegistrationProvider'
        uses 'org.hibernate.service.spi.ServiceContributor'
        uses 'org.hibernate.boot.spi.MetadataSourcesContributor'
        uses 'org.hibernate.boot.spi.MetadataBuilderInitializer'
        uses 'org.hibernate.boot.spi.MetadataBuilderFactory'
        uses 'org.hibernate.boot.model.TypeContributor'
        uses 'org.hibernate.boot.spi.MetadataContributor'
        uses 'org.hibernate.boot.spi.AdditionalJaxbMappingProducer'
        uses 'org.hibernate.boot.spi.SessionFactoryBuilderFactory'
        uses 'org.hibernate.service.spi.SessionFactoryServiceContributor'
        uses 'org.hibernate.event.spi.EventEngineContributor'
        uses 'org.hibernate.engine.transaction.jta.platform.spi.JtaPlatformProvider'
        requires 'jdk.unsupported'
    }

    forceMerge('byte-buddy')

    imageName = packageName.toString()
}
